plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.3.6'
    id 'io.spring.dependency-management' version '1.1.6'
}

group = 'io.github.wlsdks'
version = '0.3.0-SNAPSHOT'
sourceCompatibility = '21'

// 라이브러리 프로젝트이므로 bootJar 비활성화
bootJar {
    enabled = false
}

jar {
    enabled = true
}

java {
    withSourcesJar()  // 소스 JAR 생성
    withJavadocJar()  // Javadoc JAR 생성
}

repositories {
    mavenCentral()
}

dependencies {
    // 메인 의존성 (compileOnly)
    compileOnly 'org.springframework.boot:spring-boot-autoconfigure'
    compileOnly 'jakarta.servlet:jakarta.servlet-api:6.1.0'
    compileOnly 'org.slf4j:slf4j-api:2.0.16'
    compileOnly 'org.springframework:spring-webmvc'
    compileOnly 'com.fasterxml.jackson.core:jackson-databind'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Spring Boot Configuration Processor
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // 테스트 의존성 (testImplementation)
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-web' // 테스트용으로만 추가
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation 'jakarta.servlet:jakarta.servlet-api:6.1.0'
}

test {
    useJUnitPlatform()
}

// Maven Publish 설정
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Spring Fortune Cookie'
                description = 'A Spring library that adds fortune messages to HTTP responses'
                url = 'https://github.com/wlsdks/spring-fortune-cookie'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'wlsdks'
                        name = 'wlsdks'
                        email = 'your.email@example.com'
                    }
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}
