import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'java-library'
    id 'org.springframework.boot' version '3.3.6'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'com.vanniktech.maven.publish' version '0.30.0'
}

group = 'io.github.wlsdks'
version = '0.3.1'
sourceCompatibility = '21'
archivesBaseName = 'fortune-cookie' // JAR 파일 이름 설정

bootJar {
    enabled = false // Spring Boot의 실행 가능한 JAR 생성 비활성화
}

jar {
    enabled = true // 일반 JAR 생성 활성화
}

// Java 컴파일 설정
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

// Javadoc 설정
tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addBooleanOption('html5', true)
}

repositories {
    mavenCentral()
}

dependencies {
    // 주요 의존성
    compileOnly 'org.springframework.boot:spring-boot-autoconfigure'
    compileOnly 'jakarta.servlet:jakarta.servlet-api:6.1.0'
    compileOnly 'org.slf4j:slf4j-api:2.0.16'
    compileOnly 'org.springframework:spring-webmvc'
    compileOnly 'com.fasterxml.jackson.core:jackson-databind'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Spring Boot Configuration Processor
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // 테스트 의존성
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation 'jakarta.servlet:jakarta.servlet-api:6.1.0'
}

test {
    useJUnitPlatform()
}

// Maven Central 배포 설정
mavenPublishing {
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)         // Maven Central 배포
    signAllPublications()                                      // PGP 서명 사용
    coordinates("io.github.wlsdks", "fortune-cookie", "0.3.1") // 새로운 Artifact ID로 설정

    pom {
        name = "Fortune Cookie"
        description = "A library that adds fortune messages to HTTP responses in Spring"
        inceptionYear = "2024"
        url = "https://github.com/wlsdks/fortune-cookie"

        licenses {
            license {
                name = "MIT License"
                url = "http://www.opensource.org/licenses/mit-license.php"
            }
        }

        developers {
            developer {
                id = "stark"
                name = "stark"
                email = "dig04059@gmail.com"
            }
        }

        scm {
            url = "https://github.com/wlsdks/fortune-cookie"
            connection = "scm:git:git://github.com/wlsdks/fortune-cookie.git"
            developerConnection = "scm:git:ssh://github.com/wlsdks/fortune-cookie.git"
        }
    }
}
